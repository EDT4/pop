import Init.Core
import Init.Prelude
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Equivalence
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Iso
import Mathlib.CategoryTheory.NatTrans
import Mathlib.Tactic.CategoryTheory.Reassoc

namespace CategoryTheory

variable {A : Type _} [Category A]
variable {B : Type _} [Category B]
variable {C : Type _} [Category C]
variable {D : Type _} [Category D]
variable (L : A â¥¤ C)
variable (R : B â¥¤ C)

structure OplaxPullback where
  left   : A
  middle : C
  right  : B
  homl : middle âŸ¶ L.obj left
  homr : middle âŸ¶ R.obj right

variable {L} {R}

namespace OplaxPullback

@[ext]
structure Hom (x y : OplaxPullback L R) where
  left   : x.left   âŸ¶ y.left
  middle : x.middle âŸ¶ y.middle
  right  : x.right  âŸ¶ y.right
  wl : x.homl â‰« L.map left  = middle â‰« y.homl := by aesop_cat
  wr : x.homr â‰« R.map right = middle â‰« y.homr := by aesop_cat

instance Hom.inhabited
  [Inhabited (OplaxPullback L R)]
  : Inhabited (OplaxPullback.Hom (default : OplaxPullback L R) default)
  := âŸ¨{ left := ğŸ™ _, right := ğŸ™ _, middle := ğŸ™ _}âŸ©

attribute [reassoc (attr := simp)] OplaxPullback.Hom.wl OplaxPullback.Hom.wr

@[simps]
abbrev Hom.id (x : OplaxPullback L R) : Hom x x := {
  left   := ğŸ™ x.left
  middle := ğŸ™ x.middle
  right  := ğŸ™ x.right
}

@[simps]
abbrev Hom.comp {x y z : OplaxPullback L R} (f : Hom x y) (g : Hom y z) : Hom x z := {
  left   := f.left   â‰« g.left
  middle := f.middle â‰« g.middle
  right  := f.right  â‰« g.right
}

@[simps]
instance category : Category (OplaxPullback L R) where
  Hom  := Hom
  id   := Hom.id
  comp := Hom.comp

section
  variable (L) (R)

  @[simps]
  def projLeft : OplaxPullback L R â¥¤ A where
    obj x := x.left
    map f := f.left

  @[simps]
  def projMid : OplaxPullback L R â¥¤ C where
    obj x := x.middle
    map f := f.middle

  @[simps]
  def projRight : OplaxPullback L R â¥¤ B where
    obj x := x.right
    map f := f.right

  @[simps]
  def llm : NatTrans (projMid L R) (projLeft L R â‹™ L) where
    app := homl

  @[simps]
  def rrm : NatTrans (projMid L R) (projRight L R â‹™ R) where
    app := homr
end

@[simps]
def lift
  (da : D â¥¤ A)
  (db : D â¥¤ B)
  (dc : D â¥¤ C)
  (pl : NatTrans dc (da â‹™ L))
  (pr : NatTrans dc (db â‹™ R))
  : D â¥¤ OplaxPullback L R
where
  obj d := {
    left   := da.obj d
    middle := dc.obj d
    right  := db.obj d
    homl   := pl.app d
    homr   := pr.app d
  }
  map f := {
    left   := da.map f
    middle := dc.map f
    right  := db.map f
  }

abbrev liftL (da : D â¥¤ A) (db : D â¥¤ B) (p : NatTrans (da â‹™ L) (db â‹™ R)) : D â¥¤ OplaxPullback L R
  := lift da db (da â‹™ L) (NatTrans.id _) p

abbrev liftR (da : D â¥¤ A) (db : D â¥¤ B) (p : NatTrans (db â‹™ R) (da â‹™ L)) : D â¥¤ OplaxPullback L R
  := lift da db (db â‹™ R) p (NatTrans.id _)

-- Alternative definition: lift (projRight _ _) (projLeft _ _) (projMid _ _) (rrm _ _) (llm _ _)
@[simps]
def flip : OplaxPullback L R â¥¤ OplaxPullback R L where
  obj o := {
    left   := o.right
    middle := o.middle
    right  := o.left
    homl   := o.homr
    homr   := o.homl
  }
  map f := {
    left   := f.right
    middle := f.middle
    right  := f.left
  }

section
  variable {Pâ‚ Pâ‚‚ : OplaxPullback L R}
  variable (f : Pâ‚ âŸ¶ Pâ‚‚)

  instance [IsIso f] : IsIso f.left   := (projLeft   L R).map_isIso f
  instance [IsIso f] : IsIso f.middle := (projMid L R).map_isIso f
  instance [IsIso f] : IsIso f.right  := (projRight  L R).map_isIso f
end

variable {x y z: OplaxPullback L R}
variable (h : x âŸ¶ y)
variable (i : x â‰… y)

-- The purpose of this is some search tactic, but why is this necessary when ext is on the structure already?
@[ext]
lemma hom_ext
  (f g : x âŸ¶ y)
  (el : f.left   = g.left)
  (em : f.middle = g.middle)
  (er : f.right  = g.right)
  : f = g
  := Hom.ext el em er

-- The fields preserve isomorphisms.
section
  @[simps!] def leftIso   : x.left   â‰… y.left   := (projLeft   L R).mapIso i
  @[simps!] def middleIso : x.middle â‰… y.middle := (projMid L R).mapIso i
  @[simps!] def rightIso  : x.right  â‰… y.right  := (projRight  L R).mapIso i
end

def flip_obj_invol {x : OplaxPullback L R} : flip.obj (flip.obj x) = x := rfl

def flip_invol : flip â‹™ flip â‰… ğŸ­ (OplaxPullback L R) where
  hom := ğŸ™ _
  inv := ğŸ™ _

-- TODO: The strict variant also holds, but is it necessary?
-- def flip_iso : Cat.of (OplaxPullback L R) â‰… Cat.of (OplaxPullback R L) where
--   hom := flip
--   inv := flip

def flip_equiv : OplaxPullback L R â‰Œ OplaxPullback R L
  := .mk flip flip flip_invol.symm flip_invol

@[simp]
lemma inv_left [IsIso h] : (inv h).left = inv h.left := by
  apply IsIso.eq_inv_of_hom_inv_id
  rw [â† category_comp_left, IsIso.hom_inv_id, category_id_left]

@[simp]
lemma inv_middle [IsIso h] : (inv h).middle = inv h.middle := by
  apply IsIso.eq_inv_of_hom_inv_id
  rw [â† category_comp_middle, IsIso.hom_inv_id, category_id_middle]

@[simp]
lemma inv_right [IsIso h] : (inv h).right = inv h.right := by
  apply IsIso.eq_inv_of_hom_inv_id
  rw [â† category_comp_right, IsIso.hom_inv_id, category_id_right]

@[simps]
def isoMk
  {x y : OplaxPullback L R}
  (l : x.left   â‰… y.left)
  (m : x.middle â‰… y.middle)
  (r : x.right  â‰… y.right)
  (hl : x.homl â‰« L.map l.hom = m.hom â‰« y.homl := by aesop_cat)
  (hr : x.homr â‰« R.map r.hom = m.hom â‰« y.homr := by aesop_cat)
  : x â‰… y
where
  hom := {
    left := l.hom
    middle := m.hom
    right := r.hom
    wl := hl
    wr := hr
  }
  inv := {
    left   := l.inv
    middle := m.inv
    right  := r.inv
    wl := by
      rw [â† L.mapIso_inv l , Iso.eq_inv_comp , â† Category.assoc , â† hl]
      simp only [Functor.mapIso_inv, Category.assoc, Iso.map_hom_inv_id, Category.comp_id]
    wr := by
      rw [â† R.mapIso_inv r , Iso.eq_inv_comp , â† Category.assoc , â† hr]
      simp only [Functor.mapIso_inv, Category.assoc, Iso.map_hom_inv_id, Category.comp_id]
    }
